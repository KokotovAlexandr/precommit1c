#Использовать tempfiles
#Использовать asserts

Перем юТест;

Процедура Инициализация()
		
КонецПроцедуры

Функция ПолучитьСписокТестов(Знач Контекст) Экспорт
	
	юТест = Контекст;
	
	ВсеТесты = Новый Массив;
	
	ВсеТесты.Добавить("Тест_ДолженРазложитьФайлОбработкиИзЗаданнойПапки");
	ВсеТесты.Добавить("Тест_ДолженРазложитьКаталог");
	ВсеТесты.Добавить("Тест_ДолженРазобратьФайлыПоЖурналуИзмененийГит");
	
	Возврат ВсеТесты;
	
КонецФункции

Процедура ПослеЗапускаТеста() Экспорт
	ВременныеФайлы.Удалить();
КонецПроцедуры

Функция ЗагрузитьИсполнителя()
	
	ИмяКаталога = ТекущийСценарий().Каталог;
	Исполнитель = ЗагрузитьСценарий(ОбъединитьПути(ИмяКаталога,"../v8files-extractor.os"));
	Лог = Логирование.ПолучитьЛог("oscript.app.v8files-extractor");
	Лог.УстановитьУровень(УровниЛога.Отладка);
	
	Возврат Исполнитель;
	
КонецФункции

Функция ПолучитьФайлТестовойОбработки()
	
	ИмяКаталога = ТекущийСценарий().Каталог;
	ИмяФайла = "Fixture";
	ИмяФайлаСРасширением = ИмяФайла+".epf";
	
	ПутьФайла = ОбъединитьПути(ИмяКаталога, ИмяФайлаСРасширением);
	ФайлОбработки = Новый Файл(ПутьФайла);
	Ожидаем.Что(ФайлОбработки.Существует(), "Исходный файл должен существовать").ЭтоИстина();
	
	Возврат ФайлОбработки;
	
КонецФункции

Процедура Тест_ДолженРазложитьФайлОбработкиИзЗаданнойПапки() Экспорт

	ФайлОбработки = ПолучитьФайлТестовойОбработки();
	
	КаталогВыгрузки = ВременныеФайлы.СоздатьКаталог();
	
	Исполнитель = ЗагрузитьИсполнителя();
	Исполнитель.РазобратьФайл(ФайлОбработки.ПолноеИмя, КаталогВыгрузки);
	
	КаталогИсходников = Новый Файл(ОбъединитьПути(КаталогВыгрузки, ФайлОбработки.ИмяБезРасширения));
	Ожидаем.Что(КаталогИсходников.Существует()).ЭтоИстина();
	Ожидаем.Что(КаталогИсходников.ЭтоКаталог(), "Должны были найти каталог с именем обработки").ЭтоИстина();
	Ожидаем.Что(КаталогСодержитИсходникиОбработки(КаталогИсходников.ПолноеИмя)).ЭтоИстина();
КонецПроцедуры

Процедура Тест_ДолженРазложитьКаталог() Экспорт

	ФайлОбработки = ПолучитьФайлТестовойОбработки();
	
	ИмяВложенногоКаталога = "1";
	
	КаталогИсходников = ВременныеФайлы.СоздатьКаталог();
	СоздатьКаталог(ОбъединитьПути(КаталогИсходников, ИмяВложенногоКаталога));
	
	КопироватьФайл(ФайлОбработки.ПолноеИмя, ОбъединитьПути(КаталогИсходников, ИмяВложенногоКаталога, ФайлОбработки.Имя));
	
	КаталогВыгрузки = ВременныеФайлы.СоздатьКаталог();
	
	Исполнитель = ЗагрузитьИсполнителя();
	Исполнитель.РазобратьКаталог(КаталогИсходников, КаталогВыгрузки);
	
	ВложенныйКаталогВыгрузки = ОбъединитьПути(КаталогВыгрузки, ИмяВложенногоКаталога);
	ФайлВложенныйКаталогИсходников = Новый Файл(ВложенныйКаталогВыгрузки);
	Ожидаем.Что(ФайлВложенныйКаталогИсходников.Существует(), "Должны были найти имя вложенного каталога").ЭтоИстина();
	Ожидаем.Что(ФайлВложенныйКаталогИсходников.ЭтоКаталог(), "Должны были найти вложенный каталог").ЭтоИстина();

	ПутьКаталогаВыгрузкиДляОбработки = ОбъединитьПути(ВложенныйКаталогВыгрузки, ФайлОбработки.ИмяБезРасширения);
	Ожидаем.Что(КаталогСодержитИсходникиОбработки(ПутьКаталогаВыгрузкиДляОбработки)).ЭтоИстина();

КонецПроцедуры

Процедура Тест_ДолженРазобратьФайлыПоЖурналуИзмененийГит() Экспорт
	
	ФайлОбработки = ПолучитьФайлТестовойОбработки();
	
	ЖурналИзмененийГит = "A	" + ФайлОбработки.Имя+"
		| M	" + "pref-" + ФайлОбработки.Имя;
	
	Исполнитель = ЗагрузитьИсполнителя();
	МассивИмен = Исполнитель.ПолучитьИменаИзЖурналаИзмененийГит(ЖурналИзмененийГит);
	
	Ожидаем.Что(МассивИмен, "Должны были найти измененный файл").Содержит(ФайлОбработки.Имя);
	Ожидаем.Что(МассивИмен, "Должны были найти измененный файл").Содержит("pref-" + ФайлОбработки.Имя);
КонецПроцедуры

Функция КаталогСодержитИсходникиОбработки(Знач Каталог)
	ВыгруженныеФайлы = НайтиФайлы(Каталог, ПолучитьМаскуВсеФайлы());
	ИменаВыгруженныхФайлов = РазвернутьМассивФайловВИменаФайлов(ВыгруженныеФайлы);
	Ожидаем.Что(ИменаВыгруженныхФайлов, "Должны были найти выгруженный файл").Содержит("renames.txt");
	Ожидаем.Что(ИменаВыгруженныхФайлов, "Должны были найти выгруженный файл").Содержит("Form");
	Ожидаем.Что(ИменаВыгруженныхФайлов, "Должны были найти выгруженный файл").Содержит("Макеты");
	Ожидаем.Что(ИменаВыгруженныхФайлов, "Должны были найти выгруженный файл").Содержит("und");
КонецФункции

Функция РазвернутьМассивФайловВИменаФайлов(Знач МассивОбъектовФайл)
	
	ИменаФайлов = Новый Массив;
	Для Каждого Файл Из МассивОбъектовФайл Цикл
		ИменаФайлов.Добавить(Файл.Имя);
	КонецЦикла;
	
	Возврат ИменаФайлов;
	
КонецФункции

Инициализация();
